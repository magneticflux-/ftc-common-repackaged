apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'

android {
    compileSdkVersion 27
    defaultConfig {
        minSdkVersion 19
        //noinspection ExpiredTargetSdkVersion
        targetSdkVersion 19
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        versionCode 46
        versionName "$ftc_version.0"

        ndk {
            abiFilters "armeabi-v7a"
        }

        consumerProguardFiles 'proguard-rules.pro'
    }
    lintOptions {
        abortOnError false
    }
}

group = 'com.github.modular-ftc'
version = android.defaultConfig.versionName
project.archivesBaseName = 'ftc-common-repackaged'

dependencies {
    implementation 'com.android.support:support-annotations:28.0.0'
    implementation 'com.google.code.gson:gson:2.8.5'

    implementation "com.github.modular-ftc:robotcore-repackaged:$ftc_version.0"
    implementation "org.first.ftc:hardware:$ftc_version"
    implementation "org.first.ftc:inspection:$ftc_version"
    implementation "org.first.ftc:robotserver:$ftc_version"
}

//For JitPack

// build a jar with source files
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    failOnError false
    source = android.sourceSets.main.java.sourceFiles
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    classpath += configurations.compile
}

// build a jar with javadoc
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}